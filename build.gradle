import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java'
}

group = 'com.hamusuke.numguesser'
version = '1.21.0'

java.sourceCompatibility = JavaVersion.VERSION_21
java.targetCompatibility = JavaVersion.VERSION_21

repositories {
    mavenCentral()
    maven {
        url "https://libraries.minecraft.net"
    }
}

dependencies {
    implementation 'io.netty:netty-all:4.1.128.Final'
    implementation 'com.google.guava:guava:33.0.0-jre'
    implementation 'org.apache.logging.log4j:log4j-core:2.19.0'
    implementation 'org.apache.commons:commons-lang3:3.18.0'
    implementation 'it.unimi.dsi:fastutil:8.5.9'
    implementation 'com.mojang:brigadier:1.0.18'
    implementation 'com.google.code.gson:gson:2.10.1'

    implementation 'com.formdev:flatlaf:3.5.1'
    implementation 'com.formdev:flatlaf-extras:3.5.1'
    implementation 'com.formdev:flatlaf-swingx:3.5.1'
    implementation 'org.swinglabs.swingx:swingx-all:1.6.5-1'
}

tasks.register('buildClient', ShadowJar) {
    setGroup("shadow")
    archiveClassifier = "client"
    from sourceSets.main.output
    from sourceSets.main.compileClasspath
    from "LICENSE"
    from "README.md"

    manifest {
        attributes "Main-Class": "com.hamusuke.numguesser.client.MainClient"
        attributes "Multi-Release": true
    }
}

tasks.register('buildServer', ShadowJar) {
    setGroup("shadow")
    archiveClassifier = "server"
    from sourceSets.main.output
    from sourceSets.main.compileClasspath
    from "LICENSE"
    from "README.md"

    manifest {
        attributes "Main-Class": "com.hamusuke.numguesser.server.MainServer"
        attributes "Multi-Release": true
    }
}
